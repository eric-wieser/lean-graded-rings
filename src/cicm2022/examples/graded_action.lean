/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser

From: https://github.com/leanprover-community/mathlib/pull/14582
-/

import cicm2022.internal.decomposition
import cicm2022.internal.graded_ring

/-! # Graded scalar multiplication -/


/-! ### External version on graded monoid -/

namespace graded_monoid

variables {Î¹ : Type*} (A : Î¹ â†’ Type*) (M : Î¹ â†’ Type*)

/-- A graded version of `has_smul`. Scalar multiplication combines grades additively, i.e.
if `a âˆˆ A i` and `m âˆˆ M j`, then `a â€¢ b` must be in `M (i + j)`-/
class ghas_smul [has_add Î¹] :=
(smul {i j} : A i â†’ M j â†’ M (i + j))

instance ghas_mul.to_ghas_mul [has_add Î¹] [ghas_mul A] : ghas_smul A A :=
{ smul := Î» _ _, ghas_mul.mul }

instance [has_add Î¹] [ghas_smul A M] : has_smul (graded_monoid A) (graded_monoid M) :=
âŸ¨Î» (x : graded_monoid A) (y : graded_monoid M), âŸ¨_, ghas_smul.smul x.snd y.sndâŸ©âŸ©

/-- A graded version of `mul_action`. -/
class gmul_action [add_monoid Î¹] [graded_monoid.gmonoid A] extends ghas_smul A M :=
(one_smul (b : graded_monoid M) : (1 : graded_monoid A) â€¢ b = b)
(mul_smul (a a' : graded_monoid A) (b : graded_monoid M) : (a * a') â€¢ b = a â€¢ a' â€¢ b)

instance gmonoid.to_gmul_action [add_monoid Î¹] [gmonoid A] :
  gmul_action A A :=
{ one_smul := Î» x, graded_monoid.gmonoid.one_mul x,
  mul_smul := graded_monoid.gmonoid.mul_assoc }

instance [add_monoid Î¹] [gmonoid A] [gmul_action A M] :
  mul_action (graded_monoid A) (graded_monoid M) :=
{ one_smul := gmul_action.one_smul,
  mul_smul := gmul_action.mul_smul }

end graded_monoid

/-! ### Internal version -/

section set_like

/-- A version of `graded_monoid.ghas_smul` for internally graded objects. -/
class set_like.has_graded_smul {Î¹ S R N M : Type*} [set_like S R] [set_like N M]
  [has_smul R M] [has_add Î¹] (A : Î¹ â†’ S) (B : Î¹ â†’ N) : Prop :=
(smul_mem : âˆ€ â¦ƒi j : Î¹â¦„ {ai bj}, ai âˆˆ A i â†’ bj âˆˆ B j â†’ ai â€¢ bj âˆˆ B (i + j))

lemma set_like.smul_mem_graded {Î¹ S R N M : Type*} [set_like S R] [set_like N M]
  [has_smul R M] [has_add Î¹] (A : Î¹ â†’ S) (B : Î¹ â†’ N) [set_like.has_graded_smul A B]
  â¦ƒi jâ¦„ {gi gj} (hi : gi âˆˆ A i) (hj : gj âˆˆ B j) :
  gi â€¢ gj âˆˆ B (i + j) :=
set_like.has_graded_smul.smul_mem hi hj

instance set_like.ghas_smul {Î¹ S R N M : Type*} [set_like S R] [set_like N M]
  [has_smul R M] [has_add Î¹] (A : Î¹ â†’ S) (B : Î¹ â†’ N) [set_like.has_graded_smul A B] :
  graded_monoid.ghas_smul (Î» i, A i) (Î» i, B i) :=
{ smul := Î» i j a b, âŸ¨(a : R) â€¢ b, set_like.has_graded_smul.smul_mem a.2 b.2âŸ© }

@[simp] lemma set_like.coe_ghas_smul {Î¹ S R N M : Type*} [set_like S R] [set_like N M]
  [has_smul R M] [has_add Î¹] (A : Î¹ â†’ S) (B : Î¹ â†’ N) [set_like.has_graded_smul A B]
  {i j : Î¹} (x : A i) (y : B j) :
  (@graded_monoid.ghas_smul.smul Î¹ (Î» i, A i) (Î» i, B i) _ _ i j x y : M) = ((x : R) â€¢ y) :=
rfl

instance set_like.has_graded_mul.to_has_graded_smul
  {Î¹ S R : Type*} [set_like S R] [monoid R] [add_monoid Î¹] (A : Î¹ â†’ S) [set_like.has_graded_mul A] :
  set_like.has_graded_smul A A :=
{ smul_mem := Î» i j ai bj hi hj, set_like.has_graded_mul.mul_mem hi hj, }

end set_like

/-! ### Graded modules -/

section gmodule

section defs

variables {Î¹ : Type*} [add_monoid Î¹] (A : Î¹ â†’ Type*) (M : Î¹ â†’ Type*)

/-- A graded version of `distrib_mul_action`. -/
class gdistrib_mul_action [graded_monoid.gmonoid A] [Î  i, add_monoid $ M i]
  extends graded_monoid.gmul_action A M :=
(smul_add {i j} (a : A i) (b c : M j) : smul a (b + c) = smul a b + smul a c)
(smul_zero {i j} (a : A i) : smul a (0 : M j) = 0)

/-- A graded version of `module`. -/
class gmodule [graded_monoid.gmonoid A] [Î  i, add_comm_monoid $ M i] [Î  i, add_comm_monoid $ A i]
  extends gdistrib_mul_action A M :=
(add_smul {i j} (a a' : A i) (b : M j) : smul (a + a') b = smul a b + smul a' b)
(zero_smul {i j} (b : M j) : smul (0 : A i) b = 0)

end defs

-- External version of graded module
open_locale direct_sum

variables {Î¹ : Type*} [add_monoid Î¹] (A : Î¹ â†’ Type*) (M : Î¹ â†’ Type*)
variables [Î  (i : Î¹), add_comm_monoid (A i)] [Î  i, add_comm_monoid $ M i]

/-- The piecewise multiplication from the `has_mul` instance, as a bundled homomorphism. -/
@[simps]
def gsmul_hom {i j} [graded_monoid.gmonoid A] [gmodule A M] : A i â†’+ M j â†’+ M (i + j) :=
{ to_fun := Î» a,
  { to_fun := Î» b, graded_monoid.ghas_smul.smul a b,
    map_zero' := gdistrib_mul_action.smul_zero _,
    map_add' := gdistrib_mul_action.smul_add _ },
  map_zero' := add_monoid_hom.ext $ Î» a, gmodule.zero_smul a,
  map_add' := Î» aâ‚ aâ‚‚, add_monoid_hom.ext $ Î» b, gmodule.add_smul _ _ _}

/-- The multiplication from the `has_smul` instance, as a bundled homomorphism. -/
def gmodule.smul_add_monoid_hom [decidable_eq Î¹] [graded_monoid.gmonoid A] [gmodule A M] :
  (â¨ i, A i) â†’+ (â¨ i, M i) â†’+ â¨ i, M i :=
direct_sum.to_add_monoid $ Î» i,
  add_monoid_hom.flip $ direct_sum.to_add_monoid $ Î» j, add_monoid_hom.flip $
    (direct_sum.of M _).comp_hom.comp $ gsmul_hom A M

section

variables [decidable_eq Î¹] [graded_monoid.gmonoid A] [gmodule A M]
instance : has_smul (â¨ i, A i) (â¨ i, M i) :=
{ smul := Î» x y, gmodule.smul_add_monoid_hom A M x y }

@[simp] lemma gmodule.smul_def
  (x : â¨ i, A i) (y : â¨ i, M i) : x â€¢ y = gmodule.smul_add_monoid_hom _ _ x y := rfl
@[simp] lemma gmodule.smul_add_monoid_hom_apply_of_of {i j} (x : A i) (y : M j) :
  gmodule.smul_add_monoid_hom A M (direct_sum.of A i x) (direct_sum.of M j y) =
  direct_sum.of M (i + j) (graded_monoid.ghas_smul.smul x y) :=
by simp [gmodule.smul_add_monoid_hom]

@[simp] lemma gmodule.of_smul_of
  {i j} (x : A i) (y : M j) :
  direct_sum.of A i x â€¢ direct_sum.of M j y =
  direct_sum.of M (i + j) (graded_monoid.ghas_smul.smul x y) :=
gmodule.smul_add_monoid_hom_apply_of_of _ _ _ _

end

open add_monoid_hom

-- Almost identical to the proof of `direct_sum.one_mul`
private lemma one_smul [decidable_eq Î¹] [direct_sum.gsemiring A] [gmodule A M] (x : â¨ i, M i) :
  (1 : â¨ i, A i) â€¢ x = x :=
suffices gmodule.smul_add_monoid_hom A M 1 = add_monoid_hom.id (â¨ i, M i),
  from add_monoid_hom.congr_fun this x,
begin
  apply direct_sum.add_hom_ext, intros i xi,
  unfold has_one.one,
  rw gmodule.smul_add_monoid_hom_apply_of_of,
  exact direct_sum.of_eq_of_graded_monoid_eq (one_smul (graded_monoid A) $ graded_monoid.mk i xi),
end

-- Almost identical to the proof of `direct_sum.mul_assoc`
private lemma mul_smul [decidable_eq Î¹] [direct_sum.gsemiring A] [gmodule A M]
  (a b : â¨ i, A i) (c : â¨ i, M i) : (a * b) â€¢ c = a â€¢ (b â€¢ c) :=
suffices (gmodule.smul_add_monoid_hom A M).comp_hom.comp (direct_sum.mul_hom A) -- `Î» a b c, (a * b) â€¢ c` as a bundled hom
       = (add_monoid_hom.comp_hom add_monoid_hom.flip_hom $                     -- `Î» a b c, a â€¢ (b â€¢ c)` as a bundled hom
             (gmodule.smul_add_monoid_hom A M).flip.comp_hom.comp (gmodule.smul_add_monoid_hom A M)).flip,
  from add_monoid_hom.congr_fun (add_monoid_hom.congr_fun (add_monoid_hom.congr_fun this a) b) c,
begin
  ext ai ax bi bx ci cx : 6,
  dsimp only [coe_comp, function.comp_app, comp_hom_apply_apply, flip_apply, flip_hom_apply],
  rw [gmodule.smul_add_monoid_hom_apply_of_of, gmodule.smul_add_monoid_hom_apply_of_of,
    direct_sum.mul_hom_of_of, gmodule.smul_add_monoid_hom_apply_of_of],
  exact direct_sum.of_eq_of_graded_monoid_eq
    (mul_smul (graded_monoid.mk ai ax) (graded_monoid.mk bi bx) (graded_monoid.mk ci cx)),
end

instance gmodule.module [decidable_eq Î¹] [direct_sum.gsemiring A] [gmodule A M] :
  module (â¨ i, A i) (â¨ i, M i) :=
{ smul := (â€¢),
  one_smul := one_smul _ _,
  mul_smul := mul_smul _ _,
  smul_add := Î» r, (gmodule.smul_add_monoid_hom A M r).map_add,
  smul_zero := Î» r, (gmodule.smul_add_monoid_hom A M r).map_zero,
  add_smul := Î» r s x, by simp only [gmodule.smul_def, map_add, add_monoid_hom.add_apply],
  zero_smul := Î» x, by simp only [gmodule.smul_def, map_zero, add_monoid_hom.zero_apply] }

end gmodule

section

-- internal version of graded module

variables {Î¹ R A M Ïƒ Ïƒ' : Type*}
variables [add_monoid Î¹] [comm_semiring R] [semiring A] [algebra R A]
variables (ğ“ : Î¹ â†’ Ïƒ') [set_like Ïƒ' A]
variables (ğ“œ : Î¹ â†’ Ïƒ)

open_locale direct_sum

namespace graded_module

include Ïƒ' A Ïƒ M

instance gdistrib_mul_action [add_monoid M] [distrib_mul_action A M]
  [set_like Ïƒ M] [add_submonoid_class Ïƒ M] [set_like.graded_monoid ğ“]
  [set_like.has_graded_smul ğ“ ğ“œ] :
  gdistrib_mul_action (Î» i, ğ“ i) (Î» i, ğ“œ i) :=
{ smul := Î» i j x y, âŸ¨(x : A) â€¢ (y : M), set_like.has_graded_smul.smul_mem x.2 y.2âŸ©,
  one_smul := Î» âŸ¨i, mâŸ©, sigma.subtype_ext (zero_add _) (one_smul _ _),
  mul_smul := Î» âŸ¨i, aâŸ© âŸ¨j, a'âŸ© âŸ¨k, bâŸ©, sigma.subtype_ext (add_assoc _ _ _) (mul_smul _ _ _),
  smul_add := Î» i j a b c, subtype.ext $ smul_add _ _ _,
  smul_zero := Î» i j a, subtype.ext $ smul_zero _ }

variables [add_comm_monoid M] [module A M] [set_like Ïƒ M] [add_submonoid_class Ïƒ' A]
  [add_submonoid_class Ïƒ M] [set_like.graded_monoid ğ“] [set_like.has_graded_smul ğ“ ğ“œ]

-- [set_like.graded_monoid ğ“] [set_like.has_graded_smul ğ“ ğ“œ] is the internal version of graded module
-- the internal version can be translated into the external version `gmodule`.
instance gmodule : gmodule (Î» i, ğ“ i) (Î» i, ğ“œ i) :=
{ smul := Î» i j x y, âŸ¨(x : A) â€¢ (y : M), set_like.has_graded_smul.smul_mem x.2 y.2âŸ©,
  add_smul := Î» i j a a' b, subtype.ext $ add_smul _ _ _,
  zero_smul := Î» i j b, subtype.ext $ zero_smul _ _,
  ..graded_module.gdistrib_mul_action ğ“ ğ“œ}

/--
The smul multiplication of `A` on `â¨ i, ğ“œ i` from `(â¨ i, ğ“ i) â†’+ (â¨ i, ğ“œ i) â†’+ â¨ i, ğ“œ i`
turns `â¨ i, ğ“œ i` into an `A`-module
-/
def is_module [decidable_eq Î¹] [graded_ring ğ“] :
  module A (â¨ i, ğ“œ i) :=
{ smul := Î» a b, direct_sum.decompose ğ“ a â€¢ b,
  ..module.comp_hom _ $
    (direct_sum.decompose_ring_equiv ğ“ : A â‰ƒ+* â¨ i, ğ“ i).to_ring_hom }

local attribute [instance] graded_module.is_module

/--
`â¨ i, ğ“œ i` and `M` are isomorphic as `A`-modules.
"The internal version" and "the external version" are isomorphism as `A`-modules.
-/
def linear_equiv [decidable_eq Î¹] [graded_ring ğ“] 
  [direct_sum.decomposition ğ“œ] :
  M â‰ƒâ‚—[A] â¨ i, ğ“œ i :=
{ to_fun := direct_sum.decompose_add_equiv ğ“œ,
  map_smul' := Î» x y, begin
    classical,
    rw [â† direct_sum.sum_support_decompose ğ“ x, map_sum, finset.sum_smul, map_sum,
      finset.sum_smul, finset.sum_congr rfl (Î» i hi, _)],
    rw [ring_hom.id_apply, â† direct_sum.sum_support_decompose ğ“œ y, map_sum, finset.smul_sum,
      map_sum, finset.smul_sum, finset.sum_congr rfl (Î» j hj, _)],
    unfold has_smul.smul,
    simp only [direct_sum.decompose_add_equiv_apply, direct_sum.decompose_coe,
      gmodule.smul_add_monoid_hom_apply_of_of],
    convert direct_sum.decompose_coe ğ“œ _,
    refl,
  end,
  .. (direct_sum.decompose_add_equiv ğ“œ) }

end graded_module

end
